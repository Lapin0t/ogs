(*|
Abstract contexts structures
============================

Our whole development utilizes well-scoped and well-typed terms. In this style, the most
common binder representation is by using lists as contexts and well-typed DeBruijn indices
as variables. This is nice and good, but sometimes we would rather have had another,
isomorphic, representation. Indeed in an intensional type theory like Coq, sometimes
crossing an isomorphism is pretty painful.

As such, we here define what it means for an alternate definition of contexts to
"behave like lists and DeBruijn indices".

This is a very natural extension to the theory of substitution by Marcelo Fiore and
Dimitri Szamozvancev, and in fact a variant of the "Nameless, Painless" approach by
Nicolas Pouillard.

Theory
------

Categorically, it very simple. Contexts are represented by a set ``C``, a distinguished
element ``âˆ…`` and a binary operation ``- +â–¶ -``. Additionally it has a map representing
variable ``ð¯ : C â†’ ð€`` where ``ð€`` is a sufficiently well-behaved category, typically a
presheaf category. We then ask that

- ``ð¯ âˆ… â‰ˆ âŠ¥``, where ``âŠ¥`` is the initial object in ``ð€`` and
- ``ð¯ (Î“ +â–¶ Î”) â‰ˆ ð¯ Î“ + ð¯ Î”`` where `+` is the coproduct in ``ð€``.

Our category of contexts is basically the image of ``ð¯``, which has the structure of a
commutative monoid. Then, given a family ``X : C â†’ ð€``, it is easy to define
assignments as::

  Î“ =[X]> Î” â‰” ð€[ ð¯ Î“ , X Î” ]

And renamings as::

  Î“ âŠ† Î” â‰” Î“ =[ ð¯ ]> Î”
        â‰” ð€[ ð¯ Î“ , ð¯ Î” ]

Assuming ``ð€`` is (co-)powered over ``Set``, the substitution tensor product and substitution
internal hom in ``C â†’ ð€`` are given by:: 

  ( X âŠ— Y ) Î“ â‰” âˆ«^Î”  X Î” Ã— (Î” =[ Y ]> Î“)
  âŸ¦ X , Y âŸ§ Î“ â‰” âˆ«_Î”  (Î“ =[ X ]> Î”) â†’ Y Î”

More generally, given a category ``ð`` (co-)powered over ``Set`` we can define the the
following functors, generalizing the substitution tensor and hom to heretogeneous
settings::

  ( - âŠ— - ) : (C â†’ ð) â†’ (C â†’ ð€) â†’ (C â†’ ð)
  ( X âŠ— Y ) Î“ â‰” âˆ«^Î”  X Î” Ã— (Î” =[ Y ]> Î“)

  âŸ¦ - , - âŸ§ : (C â†’ ð€) â†’ (C â†’ ð) â†’ (C â†’ ð)
  âŸ¦ X , Y âŸ§ Î“ â‰” âˆ«_Î”  (Î“ =[ X ]> Î”) â†’ Y Î”

Concretely
----------  

We here apply the above theory to the case where ``ð€`` is the family category ``T â†’ Set``
for some set ``T``. Taking ``T`` to the set of types of some object language, families
``C â†’ ð€``, ie ``C â†’ T â†’ Set``, model nicely well-scoped and well-typed families. To
capture non-typed syntactic categories, indexed only by a scope, we develop the special
case where ``ð`` is just ``Set``.

We then instanciate this abstract structure with the usual lists and DeBruijn indices,
but also with two useful instances: the direct sum, where the notion of variables ``ð¯``
is the pointwise coproduct and the subset, where the notion of variables is preserved.

In further work we wish to tacle this in more generality, notable treating the case for
the idiomatic presentation of well-scoped untyped syntax where ``ð€`` is ``Set``, ``C``
is ``â„•`` and ``ð¯`` is ``Fin``. Currently, we do treat untyped syntax, but using the
non-idiomatic "unityped" presentation.

.. coq:: none
|*)
From OGS Require Import Prelude.
From OGS.Utils Require Import Psh Rel.

#[global] Declare Scope ctx_scope.
#[global] Delimit Scope ctx_scope with ctx.
#[local] Open Scope ctx_scope.

Reserved Notation "âˆ…".
Reserved Notation "Î“ +â–¶ Î”" (at level 50, left associativity).
Reserved Notation "Î“ âˆ‹ x" (at level 60).
Reserved Notation "[ x ]".
(*|
The first part of the context structure: the distinguished empty context, the binary
concatenation and the family of variables.
|*)
Class context (T C : Type) := {
  c_emp : C ;
  c_cat : C -> C -> C ;
  c_var : C -> T -> Type ;
}.
#[global] Notation "âˆ…" := c_emp : ctx_scope.
#[global] Notation "Î“ +â–¶ Î”" := (c_cat Î“ Î”) : ctx_scope.
#[global] Notation "Î“ âˆ‹ t" := (c_var Î“%ctx t).
(*|
Next we need to formalize the isomorphisms stating that variables map the empty context
to the initial family and the concatenation to the coproduct family. To do this we will
not write the usual forward map and backward map that compose to the identity, but we
will rather formalize injections with inhabited fibers. The reason for this equivalent
presentation is that it will ease dependent pattern matching by providing the isomorphisms
as "views" (see "The view from the left" by Conor McBride).

First lets define the two injection maps.
|*)
Class context_cat_wkn (T C : Type) {CC : context T C} := {
  r_cat_l {Î“ Î”} [t] : Î“ âˆ‹ t -> Î“ +â–¶ Î” âˆ‹ t ;
  r_cat_r {Î“ Î”} [t] : Î” âˆ‹ t -> Î“ +â–¶ Î” âˆ‹ t ;
}.
(*|
Then, assuming such a structure, we define two inductive families characterizing the
fibers.
|*)
Section with_param.
  Context `{C : context_cat_wkn X T}.

  Variant c_emp_view t : âˆ… âˆ‹ t -> Type := .
(*| .. coq:: none |*)
  Derive NoConfusion NoConfusionHom for c_emp_view.
(*||*)
  Variant c_cat_view Î“ Î” t : Î“ +â–¶ Î” âˆ‹ t -> Type :=
  | Vcat_l (i : Î“ âˆ‹ t) : c_cat_view Î“ Î” t (r_cat_l i)
  | Vcat_r (j : Î” âˆ‹ t) : c_cat_view Î“ Î” t (r_cat_r j)
  .
(*| .. coq:: none |*)
  #[global] Arguments Vcat_l {Î“ Î” t} i.
  #[global] Arguments Vcat_r {Î“ Î” t} j.
  Derive NoConfusion for c_cat_view.
(*||*)
End with_param.
(*|
Finally we give the rest of the laws: functions witnessing the inhabitedness of the fibers
and the injectivity of the two injection maps. This last statement is split into 3 as we
have separated the embedding for coproducts into two separate maps.
|*)
Class context_laws T C {CC : context T C} := {
  c_var_cat :: context_cat_wkn T C ;
  c_view_emp {t} i : c_emp_view t i ;
  c_view_cat {Î“ Î” t} i : c_cat_view Î“ Î” t i ;
  r_cat_l_inj {Î“ Î” t} : injective (@r_cat_l _ _ _ _ Î“ Î” t) ;
  r_cat_r_inj {Î“ Î” t} : injective (@r_cat_r _ _ _ _ Î“ Î” t) ;
  r_cat_disj {Î“ Î” t} (i : Î“ âˆ‹ t) (j : Î” âˆ‹ t) : Â¬ (r_cat_l i = r_cat_r j) ;
} .
